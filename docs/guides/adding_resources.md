---
page_title: "Add Resources to StrongDM"
---

# Add Resources to StrongDM

Your infrastructure may include various resources, such as databases, servers, clusters, or websites. With StrongDM, you can authenticate and gain secure access to these various tools. For more information, see the [Admin UI guide](https://www.strongdm.com/docs/admin-ui-guide/infrastructure-management/) in our documentation.

Our Terraform provider helps to automate the creation and management of the different resource types that make up your StrongDM environment.

To see AWS and Azure-specific onboarding examples, check the [StrongDM with Terraform and AWS](https://github.com/strongdm/terraform-sdm-onboarding/tree/master/onboarding) and [StrongDM with Terraform and Azure](https://github.com/strongdm/SDM-Azure-Terraform-Onboarding/tree/main/onboarding) quick start guides.

## Add a MySQL Database

The following example [adds a MySQL database](https://www.strongdm.com/docs/admin-ui-guide/infrastructure-management/datasources/datasource-mysql/) in the StrongDM Admin UI using the Terraform provider. For information about adding other database types, as well as argument and attribute references, see the [sdm_resource](https://registry.terraform.io/providers/strongdm/sdm/latest/docs/resources/resource) page.

### Example Usage

```hcl
# Declare variables for database username and password
variable "mysql_admin" {
  default = "strongdm-admin"
}

variable "mysql_pass" {
  default = "password"
}

# Create MySQL database in StrongDM
resource "sdm_resource" "mysql" {
  mysql {
    # Display name
    name = "example-mysql-db"
    # Hostname for database server, must be accessible to a gateway or relay
    hostname = "example.strongdm.com"
    # Username to utilize when connecting to datasource
    username = var.mysql_admin
    # Password to use when connecting to datasource
    password = var.mysql_pass
    # Database name
    database = "mysql"
    # Port to use when connecting to datasource
    port = 3306
  }
}
```

## Add an SSH Server (Public Key)

The following example [adds an SSH (Public Key) server](https://www.strongdm.com/docs/admin-ui-guide/infrastructure-management/servers/add-ssh-public-key-server/) type in the StrongDM Admin UI using the Terraform provider.

SSH connections are authenticated using an RSA public/private key pair. This key is generated by StrongDM when the resource is created. StrongDM retains the private key and only exposes the public key, which can be installed in the target server's `~/.ssh/authorized_keys` file.

For information about adding other server types, as well as argument and attribute references, see the [sdm_resource](https://registry.terraform.io/providers/strongdm/sdm/latest/docs/resources/resource) page.

### Example Usage

```hcl
# Create SSH server in StrongDM
resource "sdm_resource" "example_ssh" {
  ssh {
    # Display name
    name = "example-ssh-server"
    # Hostname for database server, must be accessible to a gateway or relay
    hostname = "example.strongdm.com"
    # Username to utilize when connecting to server
    username = "admin"
    # Port StrongDM uses to listen for connections to server
    port = 22
  }
}

# Output SSH token for use in other modules
# Accessible to other modules with module.<MODULE-NAME>.example_ssh_public_key
output "example_ssh_public_key" {
  value = sdm_resource.example_ssh.ssh[0].public_key
}
```

## Add an RDP Server

The following example [adds an RDP server](https://www.strongdm.com/docs/admin-ui-guide/infrastructure-management/servers/add-rdp/) type in the StrongDM Admin UI using the Terraform provider. For information about adding other server types, as well as argument and attribute references, see the [sdm_resource](https://registry.terraform.io/providers/strongdm/sdm/latest/docs/resources/resource) page.

### Example Usage

```hcl
# Create RDP server in StrongDM
resource "sdm_resource" "example_win_server_2019" {
  rdp {
    # Display name
    name = "example-windows-server-2019"
    # IP/DNS address used to connect to server from gateway or relay
    hostname = "windows-server.strongdm.com"
    # Username to utilize when connecting to server
    username = "admin"
    # Password to use when connecting to server
    password = "password"
    # Port StrongDM uses to listen for connections to server
    port = 3389
  }
}
```

## Add a Kubernetes Cluster (Amazon EKS)

The following example [adds an Amazon Elastic Kubernetes (EKS) cluster](https://www.strongdm.com/docs/admin-ui-guide/infrastructure-management/clusters/add-eks/) using the StrongDM provider in conjunction with the [AWS Terraform provider](https://registry.terraform.io/providers/hashicorp/aws/latest/docs). For information about adding other cluster types, as well as argument and attribute references, see the [sdm_resource](https://registry.terraform.io/providers/strongdm/sdm/latest/docs/resources/resource) page.

### Example Usage

Install both the StrongDM and AWS Terraform providers. Then continue with the following example, where we use direct credentials to configure the StrongDM provider. However, to configure the StrongDM Terraform provider, we typically recommend using [environment variables](https://registry.terraform.io/providers/strongdm/sdm/latest/docs/guides/adding_resources#environment-variables).

```hcl
# Configure all providers
# Use direct credentials for sdm
provider "sdm" {
  api_access_key = "v3PSWvI..."
  api_secret_key = "zlq0Nhn..."
}

provider "aws" {
  region     = "us-west-1"
  access_key = "AKIA2NO..."
  secret_key = "YB9Ye/o..."
}

# Declare variables to store values for AWS keys
variable "user_access_key_id" {
  default = "AKIA2NO..."
}

variable "user_access_secret_key" {
  default = "YB9Ye/o..."
}

# Retrieve information from existing EKS cluster
data "aws_eks_cluster" "myeks" {
  # Name of existing EKS cluster
  name = "my-eks-cluster"
}

# Create EKS cluster in StrongDM
resource "sdm_resource" "example_eks_cluster" {
  amazon_eks {
    # Display name
    name = "example-eks-cluster"
    cluster_name = "example-eks-cluster"
    # Retrieve endpoint from existing EKS cluster
    endpoint = replace(data.aws_eks_cluster.myeks.endpoint, "/https[[:punct:]][[:punct:]][[:punct:]]/", "")
    region = "us-west-1"
    # Retrieve CA from existing EKS cluster
    certificate_authority = base64decode(data.aws_eks_cluster.myeks.certificate_authority.0.data)
    # Get AWS key values from variables
    access_key = var.user_access_key_id
    secret_access_key = var.user_access_secret_key
    healthcheck_namespace = "default"
  }
}
```

## Add an HTTP (No Auth) Website

The following example [adds an HTTP website](https://www.strongdm.com/docs/admin-ui-guide/infrastructure-management/websites/) resource in the StrongDM Admin UI using the Terraform provider. For information about adding different website types, as well as argument and attribute references, see the [sdm_resource](https://registry.terraform.io/providers/strongdm/sdm/latest/docs/resources/resource) page.

### Example Usage

```hcl
# Create HTTP (no auth) website in StrongDM
resource "sdm_resource" "web_page" {
  http_no_auth {
    # Display name
    name = "example-http-page-1"
    # Base URL 
    url = "https://example.com/"
    # Used as your local DNS address
    subdomain = "simple-web-page"
    # Serves as starting page when accessing resource
    default_path = "/phpinfo.php"
    # Change healthcheck from default path to more specific URL
    healthcheck_path = "/phpinfo.php"
  }
}
```
